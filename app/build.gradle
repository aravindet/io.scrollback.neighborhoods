apply plugin: 'com.android.application'

def downloadAssets(String target) {
    exec {
        executable "../scrollbacklib/update.sh"
        args "../app/src/" + target, "io.scrollback.neighborhoods", "Constants.java"
    }
}

task productionAssets << {
    downloadAssets("production")
}

task stagingAssets << {
    downloadAssets("staging")
}

task developmentAssets << {
    downloadAssets("development")
}

tasks.whenTaskAdded { task ->
    if (task.name == 'generateProductionDebugAssets' || task.name == 'generateProductionReleaseAssets') {
        task.dependsOn(productionAssets)
    }

    if (task.name == 'generateStagingDebugAssets' || task.name == 'generateStagingReleaseAssets') {
        task.dependsOn(stagingAssets)
    }

    if (task.name == 'generateDevelopmentDebugAssets' || task.name == 'generateDevelopmentReleaseAssets') {
        task.dependsOn(developmentAssets)
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId 'io.scrollback.neighborhoods'
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 13
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        production {
        }

        staging {
            applicationId "io.scrollback.neighborhoods.staging"
        }

        development {
            applicationId "io.scrollback.neighborhoods.development"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:recyclerview-v7:22.1.1'
    compile 'com.facebook.stetho:stetho:1.1.1'
    compile project(":scrollbacklib")
}